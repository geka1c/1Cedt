Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;

	Получить_ТекстЗапроса_Транзит(Запрос, ТекстыЗапроса, Регистры);
	Получить_ТекстЗапроса_Возвраты(Запрос, ТекстыЗапроса, Регистры);
	Получить_ТекстЗапроса_ОстаткиТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	
КонецПроцедуры


Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.УстановитьПараметр("Период",                        ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Номер",                         ДокументСсылка.Номер);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",       ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъекта()));
	Запрос.УстановитьПараметр("Комментарий",                   ДокументСсылка.Комментарий);
	Запрос.УстановитьПараметр("ПометкаУдаления",               ДокументСсылка.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                      ДокументСсылка.Проведен);
	
КонецПроцедуры





Функция Получить_ТекстЗапроса_Транзит(Запрос, ТекстыЗапроса, Регистры) Экспорт
	ИмяРегистра = "Транзит";
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	

		ТекстЗапроса = "ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата КАК Период,
		|	ТаблицаДокумента.Покупка КАК ПокупкаСсылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДокумента.МестоХранения КАК МестоХранения,
		|	ТаблицаДокумента.Габарит КАК Габарит,
		|	ТаблицаДокумента.Участник КАК Участник,
		|	ТаблицаДокумента.ПунктВыдачи КАК Точка,
		|	ТаблицаДокумента.Партия КАК Партия,
		|	ТаблицаДокумента.Количество КАК Количество
		|ИЗ
		|	Документ.ДвижениеТранзита.ПокупкиТранзит КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата КАК Период,
		|	ТаблицаДокумента.Покупка КАК ПокупкаСсылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ТаблицаДокумента.МестоХраненияНазначение КАК МестоХранения,
		|	ТаблицаДокумента.ГабаритНазначение КАК Габарит,
		|	ТаблицаДокумента.Участник КАК Участник,
		|	ТаблицаДокумента.ПунктВыдачиНазначение КАК Точка,
		|	ТаблицаДокумента.Партия КАК Партия,
		|	ТаблицаДокумента.Количество КАК Количество
		|ИЗ
		|	Документ.ДвижениеТранзита.ПокупкиТранзит КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	и ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОпераций_ДвижениеТранзита.ТранзитНаТранзит)";
	

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции


Функция Получить_ТекстЗапроса_Возвраты(Запрос, ТекстыЗапроса, Регистры) Экспорт
	ИмяРегистра = "Возвраты";
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	

		ТекстЗапроса = "
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата 			КАК Период,
		|	ТаблицаДокумента.Покупка 				КАК Покупка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 	КАК ВидДвижения,
		|	ТаблицаДокумента.МестоХраненияНазначение	КАК МестоХранения,
		|	ТаблицаДокумента.Организатор			КАК Организатор,
		|	ТаблицаДокумента.Участник 				КАК Участник,
		|	ТаблицаДокумента.Партия 				КАК Партия,
		|	ТаблицаДокумента.Количество 			КАК Количество
		|ИЗ
		|	Документ.ДвижениеТранзита.ПокупкиТранзит КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка и
		| 	ТаблицаДокумента.Ссылка.ВидОперации = Значение(Перечисление.ВидыОпераций_ДвижениеТранзита.ТранзитНаВозврат)
		|
		|";
	

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция Получить_ТекстЗапроса_ОстаткиТоваров(Запрос, ТекстыЗапроса, Регистры) Экспорт
	ИмяРегистра = "ОстаткиТоваров";
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	

		ТекстЗапроса = "
		|
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка.Дата 			КАК Период,
		|	ТаблицаДокумента.Покупка 				КАК Покупка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 	КАК ВидДвижения,
		|	ТаблицаДокумента.МестоХраненияНазначение 			КАК МестоХранения,
		|	ТаблицаДокумента.ГабаритНазначение				КАК Габарит,
		|	Истина									КАК Оплачен,
		|	ТаблицаДокумента.Участник 				КАК Участник,
		|	ТаблицаДокумента.Партия 				КАК Партия,
		|	ТаблицаДокумента.Количество 			КАК Количество
		|ИЗ
		|	Документ.ДвижениеТранзита.ПокупкиТранзит КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка и
		| 	ТаблицаДокумента.Ссылка.ВидОперации = Значение(Перечисление.ВидыОпераций_ДвижениеТранзита.ТранзитНаОстатки)
		|
		|";
	

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции




Функция ПолноеИмяОбъекта()
	Возврат "Документ.ДвижениеТранзита";
КонецФункции
